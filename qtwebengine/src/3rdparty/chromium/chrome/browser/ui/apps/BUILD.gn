# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//extensions/buildflags/buildflags.gni")
import("//media/media_options.gni")

assert(is_win || is_mac || is_linux || is_chromeos)

source_set("apps") {
  sources = [
    "app_info_dialog.h",
    "chrome_app_delegate.h",
    "chrome_app_window_client.h",
    "directory_access_confirmation_dialog.h",
  ]

  public_deps = [
    "//base",
    "//ui/base:types",
    "//ui/gfx/geometry",
  ]
  if (enable_extensions) {
    public_deps += [
      "//extensions/browser",
    ]
  }
}

source_set("impl") {
  sources = [
    "chrome_app_delegate.cc",
    "chrome_app_window_client.cc",
    "directory_access_confirmation_dialog.cc",
  ]

  deps = [
    ":apps",
    "//build:chromeos_buildflags",
    "//chrome/app:generated_resources",
    "//chrome/app:generated_resources_grit",
    "//chrome/browser:primitives",
    "//chrome/browser/devtools",
    "//chrome/browser/favicon",
    "//chrome/browser/file_system_access",
    "//chrome/browser/picture_in_picture",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui:browser_navigator_params_headers",
    "//components/keep_alive_registry",
    "//components/version_info",
    "//components/zoom",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//printing/buildflags",
    "//services/service_manager/public/cpp",
    "//ui/base",
  ]

  if (enable_webrtc) {
    deps += [
      "//chrome/browser/media/webrtc",
    ]
  }

  if (enable_extensions) {
    deps += [
      "//chrome/browser/apps/platform_apps",
      "//extensions/common",
      "//extensions/common:common_constants",
      "//extensions/common:mojom",
    ]
  }
  if (is_chromeos_ash) {
    deps += [ "//chrome/browser/ash/lock_screen_apps:lock_screen_apps" ]
  }

  if (is_chromeos) {
    deps += [ "//chrome/browser/chromeos/policy/dlp" ]
  }
}
